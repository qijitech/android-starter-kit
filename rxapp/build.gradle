import com.android.builder.core.BuilderConstants

apply plugin: 'com.android.application'

android {
  compileSdkVersion rootProject.ext.android.compileSdkVersion
  buildToolsVersion rootProject.ext.android.buildToolsVersion

  defaultConfig {
    applicationId "starter.kit.rx.app"
    minSdkVersion rootProject.ext.android.minSdkVersion
    targetSdkVersion rootProject.ext.android.targetSdkVersion
    versionCode 1
    versionName "1.0"
  }

  packagingOptions {
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/rxjava.properties'
  }

  buildTypes {
     debug {
      applicationIdSuffix ".debug" // 为了不和 release 版本冲突
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    // if true, only report errors
    ignoreWarnings true
    abortOnError false
  }

  compileOptions {
    sourceCompatibility rootProject.ext.android.javaVersion
    targetCompatibility rootProject.ext.android.javaVersion
  }

  applicationVariants.all { variant ->
    if (variant.buildType.name == BuilderConstants.RELEASE) {
      variant.mergedFlavor.versionCode = gitVersionCode()
      variant.mergedFlavor.versionName = gitVersionName()

      variant.outputs.each { fileObj ->
        def outputFile = fileObj.outputFile
        if (outputFile != null && outputFile.name.endsWith('.apk')) {
          //def fileName = "rxjava_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
          def fileName = "${parent.name}_v${defaultConfig.versionName}_${releaseTime()}_${variant.buildType.name}.apk"
//          fileObj.outputFile = new File(releaseOutputFile(), fileName)
        }
      }
    }
  }
}

repositories {
  jcenter()
  maven { url "https://clojars.org/repo/" }
  maven { url "https://jitpack.io" }
  maven { url "https://dl.bintray.com/qijitech/android/" }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation deps.parceler
  annotationProcessor deps.parcelerProcessor

  implementation 'com.github.Doublemine:SimpleHud:0.1.2'
  implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

  implementation 'com.github.bumptech.glide:glide:3.7.0'

  implementation('com.mikepenz:materialdrawer:5.3.6@aar') {
    transitive = true
  }
  implementation 'com.mikepenz:google-material-typeface:2.2.0.1@aar'
  implementation 'com.mikepenz:fontawesome-typeface:4.4.0.1@aar'
  implementation 'com.mikepenz:octicons-typeface:3.0.0.1@aar'

  implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1'

  implementation deps.fresco
  implementation deps.recyclerviewFlexibleDivider

  implementation deps.appcompatV7
  implementation deps.cardviewV7

  // Recycler View
  implementation deps.recyclerviewV7
  implementation deps.paginate

  implementation deps.converterJackson

  implementation deps.materialProgress

  // Square
  implementation deps.retrofit
  implementation deps.okhttp
  implementation deps.loggingInterceptor

  // rx
  implementation deps.rxandroid
  // Because RxAndroid releases are few and far between, it is recommended you also
  // explicitly depend on RxJava's latest version for bug fixes and new features.
  implementation deps.rxjava
  implementation deps.adapterRxjava

  implementation deps.rxlifecycle
  implementation deps.rxlifecycleAndroid

  implementation deps.logger

  implementation deps.icepick
  annotationProcessor deps.icepickProcessor

  implementation deps.butterknife
  annotationProcessor deps.butterknifeCompiler

  implementation deps.multidex

  implementation project(':rxlibrary')
  implementation project(':support-ui-core')
  implementation project(':support-ui-adapters')
  implementation project(':support-ui-cells')
  implementation project(':support-ui-widget')
  //  implementation 'com.smartydroid:rxstarter:0.1'
}

static def gitVersionCode() {
  def cmd = 'git rev-list HEAD --all  --count'
  def versionCode = cmd.execute().text.trim()
  if (versionCode.empty) {
    return 1
  }
  return versionCode.toInteger()
}

static def gitVersionName() {
  def cmd = 'git describe --abbrev=0 --tags'
  def versionName = cmd.execute().text.trim()
  if (versionName.length() > 5 || versionName.length() <= 0) {
    return "1.0";
  }
  return versionName;
}

static def releaseTime() {
  return new Date().format('yyyyMMddHHmm', TimeZone.getDefault())
}

static def releaseOutputFile() {
  return '../release/'
}
